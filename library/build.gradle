/*
 * Copyright (c) 2017. Jahir Fiquitiva. Android Developer. All rights reserved.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    TARGET_SDK = 25
    BUILD_TOOLS = "25.0.2"
    ANDROID_SUPPORT_LIBS = "25.1.1"
}

android {
    compileSdkVersion TARGET_SDK
    buildToolsVersion BUILD_TOOLS

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "2g"
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion TARGET_SDK
        versionCode 1
        versionName "0.0.1"
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// apply from: 'fat-aar.gradle'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:support-v4:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:design:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:recyclerview-v7:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:cardview-v7:$ANDROID_SUPPORT_LIBS"
    // Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'
    // Drawer
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    //OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    //Glide Library - Image Loading Lib
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //RecyclerView Fast Scroller
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    //Circular Image View
    compile 'de.hdodenhof:circleimageview:2.1.0'
    //Muzei API
    //noinspection GradleDynamicVersion
    compile 'com.google.android.apps.muzei:muzei-api:2.0'
    //License Checker Lib
    compile 'com.github.javiersantos:PiracyChecker:0.0.3'
    // embedded project(':repmax')
    /*
    // Icons
    compile "com.mikepenz:iconics-core:2.8.2@aar"
    compile 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    */
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}